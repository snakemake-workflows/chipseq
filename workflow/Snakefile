# The main entry point of workflow.
# After configuring, running snakemake -n in a clone of this repository should successfully execute a dry-run of the workflow.

include: "rules/common.smk"
include: "rules/ref.smk"
include: "rules/qc.smk"
include: "rules/cutadapt.smk"
include: "rules/mapping.smk"
include: "rules/filtering.smk"
include: "rules/stats.smk"
include: "rules/utils.smk"
include: "rules/post-analysis.smk"

def all_input(wildcards):

    wanted_input = []

    # QC with fastQC and multiQC
    wanted_input.extend(["results/qc/multiqc/multiqc.html"])

    # trimming reads
    for (sample, unit) in units.index:
        if is_single_end(sample, unit):
            wanted_input.extend(expand(
                    [
                        "results/trimmed/{sample}-{unit}.fastq.gz",
                        "results/trimmed/{sample}-{unit}.se.qc.txt"
                    ],
                    sample = sample,
                    unit = unit
                )
            )
        else:
            wanted_input.extend(
                expand (
                    [
                        "results/trimmed/{sample}-{unit}.1.fastq.gz",
                        "results/trimmed/{sample}-{unit}.2.fastq.gz",
                        "results/trimmed/{sample}-{unit}.pe.qc.txt"
                    ],
                    sample = sample,
                    unit = unit
            )
        )

    # mapping, merging and filtering bam-files
    for sample in samples.index:
        wanted_input.extend(
            expand (
                [
                    "results/IGV/merged_library.bigWig.igv.txt",
                    "results/deeptools/plot_profile.pdf",
                    "results/deeptools/heatmap.pdf",
                    "results/deeptools/heatmap_matrix.tab",
                    "results/phantompeakqualtools/{sample}.phantompeak",
                    "results/phantompeakqualtools/{sample}.phantompeak.Rdata",
                    "results/phantompeakqualtools/{sample}.phantompeak.pdf"
                ],
                sample = sample
            )
        )

    return wanted_input

rule all:
    input: all_input
